dnl Process this file with autoconf to produce a configure script.
dnl
dnl Change the version number here
AC_INIT([Evolving Objects], [0.9.4-cvs], [eodev-help@sourceforge.net], [eo])
AC_PREREQ(2.53)

dnl make sure we are compiling from the correct sources
AC_CONFIG_SRCDIR(src/eoOp.h)

dnl define host, build, and target machine
AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

dnl automake initialization
AM_INIT_AUTOMAKE([gnu dist-bzip2 dist-zip])
AM_CONFIG_HEADER(config.h)

dnl user-switches
AC_APPLICATIONS
AC_TUTORIAL

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_ARG_VAR([DOXYGEN], [doxygen - automatic documentation generator])
AC_CHECK_PROGS([DOXYGEN], [doxygen], [true])
AC_ARG_VAR([AR], [ar - archiver for static libraries])
# solaris-x86 hides ar at "/usr/ccs/bin/ar"
AC_CHECK_PROGS([AR], [ar /usr/ccs/bin/ar], [false])

dnl Checks for compiler characteristics.
AC_CXX_NAMESPACES
AC_TYPE_SIZE_T

dnl Checks for header files.
AC_LANG(C++)
AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h)
AC_CHECK_HEADERS(sstream, [], AC_MSG_ERROR([Need sstream C++ include.]))
AC_CHECK_HEADERS(stdint.h, [], AC_MSG_WARN([Need C99 standard header.]))
AC_CXX_HAVE_NUMERIC_LIMITS

dnl Checks for typedefs, structures, and compiler characteristics.
AC_CHECK_SIZEOF(unsigned long)
AC_CHECK_TYPES(uint32_t, [], AC_MSG_WARN([Need uint32_t from C99 standard.]))

dnl Checks for libraries.

dnl Checks for library functions.
AC_CHECK_LIB(m, cos)

dnl create makefiles
AC_OUTPUT(Makefile \
          app/Makefile \
          app/mastermind/Makefile \
          app/gprop/Makefile \
          app/gpsymreg/Makefile \
          contrib/Makefile \
          doc/Makefile \
          src/Makefile \
          src/do/Makefile \
          src/es/Makefile \
          src/gp/Makefile \
          src/ga/Makefile \
          src/other/Makefile \
          src/utils/Makefile \
          test/Makefile \
          tutorial/Makefile \
          tutorial/Lesson1/Makefile \
          tutorial/Lesson2/Makefile \
          tutorial/Lesson3/Makefile \
          tutorial/Lesson4/Makefile \
          tutorial/Lesson5/Makefile \
          win/Makefile)
